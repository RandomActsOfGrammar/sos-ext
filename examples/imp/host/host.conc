Module imp:host

ignore   /\n\r\t /

name_t   /[a-z][a-zA-Z0-9_]*/
int_t    /[0-9]+/

eq_t      /=/
deq_t     /==/
plus_t    /\+/
or_t      /\|\|/
and_t     /&&/
gt_t      />/

true_t    /true/
false_t   /false/

noop_t    /noop/
while_t   /while/
do_t      /do/
if_t      /if/
then_t    /then/
else_t    /else/
end_t     /end/
seq_t     /;/


a_conc<a> ::= L::a_conc plus_t R::a_conc ~~> plus(L, R)
            | T::a2_conc ~~> T
a2_conc<a> ::= I::int_t ~~> num($to_int(I))
             | Name::name_t ~~> name(Name)

b_conc<b> ::= L::b_conc or_t R::b_conc ~~> or(L, R)
            | T::b2_conc ~~> T
b2_conc<b> ::= L::b_conc and_t R::b_conc ~~> and(L, R)
             | T::b3_conc ~~> T
b3_conc<b> ::= L::a_conc gt_t R::a_conc ~~> greater(L, R)
             | L::a_conc deq_t R::a_conc ~~> eq(L, R)
             | true_t ~~> true
             | false_t ~~> false

c_conc<c> ::= noop_t ~~> noop
            | Name::name_t eq_t A::a_conc seq_t ~~> assign(Name, A)
            | if_t Cond::b_conc then_t Th::c_conc else_t El::c_conc end_t ~~>
                 ifThenElse(Cond, Th, El)
            | while_t Cond::b_conc do_t Body::c_conc end_t ~~>
                 while(Cond, Body)
            | L::c_conc R::c_conc ~~> seq(L, R)
